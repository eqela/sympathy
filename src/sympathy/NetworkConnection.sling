
/*
 * This file is part of Sympathy
 * Copyright (c) 2017 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class abstract imports capex.net:

prop storageIndex as int
var ioEntry as IOManagerEntry
var id as long
var logContext protected as LoggingContext
var socket protected as ConnectedSocket
var idcounter static = 0
var lastActivity protected as long
var remoteAddress as string
var currentListenMode = -1
var manager as NetworkConnectionManager
prop defaultListenMode = 1

ctor
{
	id = idcounter ++
}

func getId as long:
	return id

func exportSocket as ConnectedSocket
{
	var v = socket
	socket = null
	return v
}

func getSocket as ConnectedSocket:
	return socket

func getManager as NetworkConnectionManager:
	return manager

func getLastActivity as long:
	return lastActivity

func getRemoteAddress as string
{
	if(remoteAddress == null) {
		var ts = socket as TCPSocket
		if(ts != null) {
			remoteAddress = ts.getRemoteAddress() .. ":" .. String.forInteger(ts.getRemotePort())
		}
	}
	return(remoteAddress)
}

func logDebug(text as string)
{
	Log.debug(logContext, "[Connection:" .. String.forInteger(getId()) .. "] " .. text)
}

func logInfo(text as string)
{
	Log.info(logContext, "[Connection:" .. String.forInteger(getId()) .. "] " .. text)
}

func logWarning(text as string)
{
	Log.warning(logContext, "[Connection:" .. String.forInteger(getId()) .. "] " .. text)
}

func logError(text as string)
{
	Log.error(logContext, "[Connection:" .. String.forInteger(getId()) .. "] " .. text)
}

func onActivity
{
	lastActivity = SystemClock.asSeconds()
}

func initialize virtual as bool
{
	return(true)
}

func cleanup virtual
{
}

func doInitialize(logContext as LoggingContext, socket as ConnectedSocket, manager as NetworkConnectionManager) as bool
{
	this.logContext = logContext
	this.socket = socket
	this.manager = manager
	if(initialize() == false) {
		return(false)
	}
	onActivity()
	return(true)
}

func getIoEntry as IOManagerEntry
{
	return(ioEntry)
}

func setIoEntry(entry as IOManagerEntry)
{
	this.ioEntry = entry
	this.currentListenMode = -1
	if(entry != null) {
		setListenMode(getDefaultListenMode())
	}
}

func sendData(data as buffer, size as int) as int
{
	if(socket == null) {
		return(0)
	}
	var v = socket.write(data, size)
	if v < 0:
		close()
	onActivity()
	return(v)
}

func close
{
	var ss = socket
	socket = null
	if ioEntry {
		ioEntry.remove()
		ioEntry = null
	}
	if ss:
		ss.close()
	if manager:
		manager.onConnectionClosed(this)
	cleanup()
	onClosed()
	this.socket = null
	this.manager = null
}

func closeSocket
{
	if socket:
		socket.close()
}

func onReadReady virtual
{
	assert socket
	var recvBuffer as buffer
	if manager:
		recvBuffer = manager.getReceiveBuffer()
	if recvBuffer == null:
		recvBuffer = MALLOC(1024)
	var n = socket.read(recvBuffer)
	if n < 0 {
		close()
	}
	else {
		onDataReceived(recvBuffer, n)
	}
	onActivity()
}

func onWriteReady virtual
{
}

func enableIdleMode:
	setListenMode(0)

func enableReadMode:
	setListenMode(1)

func enableWriteMode:
	setListenMode(2)

func enableReadWriteMode:
	setListenMode(3)

func setListenMode(n as int) private
{
	if ioEntry == null {
		defaultListenMode = n
		return
	}
	if n == currentListenMode:
		return
	currentListenMode = n
	if n == 0 {
		ioEntry.setListeners(null, null)
	}
	else if n == 1 {
		ioEntry.setListeners(onReadReady, null)
	}
	else if n == 2 {
		ioEntry.setListeners(null, onWriteReady)
	}
	else if n == 3 {
		ioEntry.setListeners(onReadReady, onWriteReady)
	}
}

func onOpened abstract
func onDataReceived(data as buffer, size as int) abstract
func onClosed abstract
func onError(message as string) abstract
