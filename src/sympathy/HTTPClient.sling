
/*
 * This file is part of Sympathy
 * Copyright (c) 2017 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class abstract imports cape:

func createDefault static as this
{
	return(new HTTPClientOperation())
}

func execute(client as this, request as HTTPClientRequest, listener as HTTPClientListener) static
{
	if(client == null) {
		return
	}
	client.executeRequest(request, listener)
}

class MyListener is HTTPClientListener imports cape
{
	prop response as HTTPClientResponse
	prop buffer as buffer

	func onResponseReceived(response as HTTPClientResponse) override as bool
	{
		this.response = response
		return(true)
	}

	func onDataReceived(buffer as buffer) override as bool
	{
		this.buffer = Buffer.append(this.buffer, buffer)
		return(true)
	}

	func onAborted override
	{
	}

	func onError(message as string) override
	{
	}

	func onResponseCompleted override
	{
	}
}

func execute(client as this, request as HTTPClientRequest, listener as function<void,HTTPClientResponse>) static
{
	var ll = new MyListener()
	execute(client, request, ll)
	if(listener != null) {
		listener(ll.getResponse())
	}
}

func executeForBuffer(client as this, request as HTTPClientRequest, listener as function<void,HTTPClientResponse,buffer>) static
{
	var ll = new MyListener()
	execute(client, request, ll)
	if(listener != null) {
		listener(ll.getResponse(), ll.getBuffer())
	}
}

func executeForString(client as this, request as HTTPClientRequest, listener as function<void,HTTPClientResponse,string>) static
{
	var ll = new MyListener()
	execute(client, request, ll)
	if(listener != null) {
		listener(ll.getResponse(), String.forUTF8Buffer(ll.getBuffer()))
	}
}

func executeRequest(request as HTTPClientRequest, listener as HTTPClientListener) abstract
