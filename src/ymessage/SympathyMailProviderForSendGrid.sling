
/*
 * This file is part of Eqela Sympathy
 * Copyright (c) 2020-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import sympathy.apiclient.sendgrid

class is SympathyMailProvider:

prop apiKey as string
var ctx as LoggingContext

ctor(ctx as LoggingContext):
	this.ctx = ctx

func send(mail as SympathyMailContent) override async
{
	var mailSender = mail.sender
	if !mailSender:
		Error.throw("noSenderObject", null)
	var sender = SendGridApiClient.EmailUser.forData(mailSender.toDynamicMap())
	var sendGrid = new SendGridApiClient(ctx) {
		apiKey apiKey
		sender sender
	}
	var rcpts = mail.recipients
	if !rcpts:
		Error.throw("noRecipientsObject", null)
	var recipients = new vector<SendGridApiClient.EmailUser>
	foreach rcpt in rcpts:
		recipients += SendGridApiClient.EmailUser.forData(rcpt.toDynamicMap())
	var contents = new vector<SendGridApiClient.EmailContent>
	var content = new SendGridApiClient.EmailContent()
	content.type = mail.contentType
	content.value = mail.content
	contents += content
	await sendGrid.sendEmail(recipients, contents, mail.subject)
}
