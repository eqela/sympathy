
/*
 * This file is part of Eqela Sympathy
 * Copyright (c) 2020-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.random
import jk.time.format
import jk.time

class:

var rcptsTo as DynamicVector
var rcptsCC as DynamicVector
var rcptsBCC as DynamicVector
var replyTo as string
var subject as string
var contentType as string
var text as string
var myName as string
var myAddress as string
var messageBody as string
var messageID as string
var date as string
pvar excludeAddresses as DynamicVector
var counter private static = 0

ctor:
	date = VerboseDateTimeString.forUTCNow()

/*
func forTemplate(template as string, data as DynamicMap = null) static as this
{
	var v = new this()
	v.initializeFromTemplate(template, data)
	return v
}

func initializeFromTemplate(template as string, data as DynamicMap = null)
{
	var tt = assert template
	if data:
		tt = TextTemplate.processString(tt, "<%", "%>", data)
	var boundary = String.getIndexOfString(tt, "\n\n")
	if boundary < 0:
		boundary = String.getIndexOfString(tt, "\r\n\r\n")
	if boundary > 0 {
		var hdr = String.getSubString(tt, 0, boundary)
		if hdr {
			foreach line in String.split(hdr, '\n') {
				line = String.strip(line)
				var comps = String.split(line, ':', 2)
				var key = String.strip(Vector.get(comps, 0))
				var val = String.strip(Vector.get(comps, 1))
				if String.isEmpty(key):
					continue
				if key == "replyTo" {
					setReplyTo(val)
				}
				else if key == "subject" {
					setSubject(val)
				}
				else if key == "contentType" {
					setContentType(val)
				}
				else if key == "myName" {
					setMyName(val)
				}
				else if key == "myAddress" {
					setMyAddress(val)
				}
				else {
					nop // ignore unknown header
				}
			}
		}
		setText(String.strip(String.getEndOfString(tt, boundary)))
	}
	else {
		setText(String.strip(tt))
	}
}
*/

func onChanged private:
	messageBody = null

func generateMessageID(host as string) as this
{
	messageID = String.forLongInteger(SystemClock.asSeconds()) .. "-" .. String.forInteger(new Random().nextIntegerWithMaximum(1000000)) .. "-" .. String.forInteger(counter) .. "@" .. host
	counter++
	onChanged()
	return this
}

func getDate as string:
	return date

func getReplyTo as string:
	return replyTo

func setDate(date as string) as this
{
	this.date = date
	onChanged()
	return this
}

func setMessageID(id as string) as this
{
	messageID = id
	onChanged()
	return this
}

func setReplyTo(v as string) as this
{
	replyTo = v
	onChanged()
	return this
}

func getMessageID as string:
	return messageID

func isExcludedAddress(add as string) private as bool
{
	if excludeAddresses == null || excludeAddresses.getSize() < 1:
		return false
	foreach ea as string in excludeAddresses.toVector() {
		if String.equals(ea, add):
			return true
	}
	return false
}

func getAllRcpts as DynamicVector
{
	var rcpts = new DynamicVector()
	if rcptsTo {
		foreach r as string in rcptsTo.toVector() {
			if isExcludedAddress(r):
				continue
			rcpts.appendObject(r)
		}
	}
	if rcptsCC {
		foreach r as string in rcptsCC.toVector() {
			if isExcludedAddress(r):
				continue
			rcpts.appendObject(r)
		}
	}
	if rcptsBCC {
		foreach r as string in rcptsBCC.toVector() {
			if isExcludedAddress(r):
				continue
			rcpts.appendObject(r)
		}
	}
	return rcpts
}

func getRcptsTo as DynamicVector:
	return rcptsTo

func getRcptsCC as DynamicVector:
	return rcptsCC

func getRcptsBCC as DynamicVector:
	return rcptsBCC

func getSubject as string:
	return subject

func getContentType as string:
	return contentType

func getText as string:
	return text

func getMyName as string:
	return myName

func getMyAddress as string:
	return myAddress

func setSubject(s as string) as this
{
	subject = s
	onChanged()
	return this
}

func setContentType(c as string) as this
{
	contentType = c
	onChanged()
	return this
}

func setText(t as string) as this
{
	text = t
	onChanged()
	return this
}

func setMyName(n as string) as this
{
	myName = n
	onChanged()
	return this
}

func setMyAddress(a as string) as this
{
	myAddress = a
	onChanged()
	return this
}

func setTo(address as string) as this
{
	rcptsTo = new DynamicVector()
	rcptsTo.appendObject(address)
	onChanged()
	return this
}

func addTo(address as string) as this
{
	if String.isNotEmpty(address) {
		if not rcptsTo:
			rcptsTo = new DynamicVector()
		rcptsTo.appendObject(address)
	}
	onChanged()
	return this
}

func addCC(address as string) as this
{
	if String.isNotEmpty(address) {
		if not rcptsCC:
			rcptsCC = new DynamicVector()
		rcptsCC.appendObject(address)
	}
	onChanged()
	return this
}

func addBCC(address as string) as this
{
	if String.isNotEmpty(address) {
		if not rcptsBCC:
			rcptsBCC = new DynamicVector()
		rcptsBCC.appendObject(address)
	}
	onChanged()
	return this
}

func setRecipients(to as DynamicVector, cc as DynamicVector, bcc as DynamicVector) as this
{
	rcptsTo = to
	rcptsCC = cc
	rcptsBCC = bcc
	onChanged()
	return this
}

func getSizeBytes as int
{
	var b = getMessageBody()
	if not b:
		return 0
	var bb = String.toUTF8Buffer(b)
	if not bb:
		return 0
	return Buffer.getSize(bb) as int
}

func getMessageBody virtual as string
{
	if messageBody:
		return messageBody
	var sb = new StringBuilder()
	sb.appendString("From: ")
	sb.appendString(myName)
	sb.appendString(" <")
	sb.appendString(myAddress)
	if String.isNotEmpty(replyTo) {
		sb.appendString(">\r\nReply-To: ")
		sb.appendString(myName)
		sb.appendString(" <")
		sb.appendString(replyTo)
	}
	sb.appendString(">\r\nTo: ")
	var first = true
	if rcptsTo {
		foreach to as string in rcptsTo.toVector() {
			if not first:
				sb.appendString(", ")
			sb.appendString(to)
			first = false
		}
	}
	sb.appendString("\r\nCc: ")
	first = true
	if rcptsCC {
		foreach to as string in rcptsCC.toVector() {
			if not first:
				sb.appendString(", ")
			sb.appendString(to)
			first = false
		}
	}
	sb.appendString("\r\nSubject: ")
	sb.appendString(subject)
	sb.appendString("\r\nContent-Type: ")
	sb.appendString(contentType)
	sb.appendString("\r\nDate: ")
	sb.appendString(date)
	if String.isNotEmpty(messageID) {
		sb.appendString("\r\nMessage-ID: <")
		sb.appendString(messageID)
		sb.appendString(">")
	}
	sb.appendString("\r\n\r\n")
	sb.appendString(text)
	messageBody = sb.toString()
	return messageBody
}
