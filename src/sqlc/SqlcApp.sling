
/*
 * This file is part of Sympathy
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License, version 3,
 * as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import jk.fs
import jk.log
import jk.sql
import jk.sql.factory
import jk.time
import jk.time.format
import jk.console
import jk.text

class is ConsoleApplicationMain:

main:
	return ConsoleApplicationMain.executeMain(new this(), args)

var connectionString as string
var sqlQuery as string
var sqlExecute as string
var params = new vector<object>
var verbose = false
var nodata = false
var listTables = false
var count = false
var stream = false
var currentOperation as string
var operationStartTime as TimeValue

func trace(msg as string)
{
	assert verbose
	Log.debug(ctx, VerboseDateTimeString.forNow() .. ": " .. msg)
}

func startOperation(name as string)
{
	trace("Start operation: " .. name)
	currentOperation = name
	operationStartTime = SystemClock.asTimeValue()
}

func endOperation
{
	var diff = StringUtil.formatLongInteger(TimeValue.diff(SystemClock.asTimeValue(), operationStartTime), ',')
	trace("End operation: " .. currentOperation .. ". Duration=" .. diff)
	operationStartTime = null
	currentOperation = null
}

func addParamsToStatement(stmt as SQLStatement) private
{
	assert stmt
	foreach param in params {
		if param is string {
			stmt.addParamString(param as! string)
		}
		else if param is IntegerObject {
			stmt.addParamInteger((param as! IntegerObject).toInteger())
		}
		else if param is LongIntegerObject {
			stmt.addParamLongInteger((param as! LongIntegerObject).toLong())
		}
		else if param is DoubleObject {
			stmt.addParamDouble((param as! DoubleObject).toDouble())
		}
		else if param is buffer {
			stmt.addParamBlob(param as! buffer)
		}
	}
}

func execute override as bool
{
	foreach arg in args {
		if arg.isFlag("help") || arg.isFlag("h") {
			ConsoleApplicationUtil.printHeader(Stdout.instance(), VALUE "sling_project_title", VALUE "sling_project_version", VALUE "sling_project_copyright", VALUE "sling_project_license", VALUE "target_platform")
			return true
		}
		else if arg.isFlag("version") || arg.isFlag("v") {
			ConsoleApplicationUtil.printVersion(Stdout.instance(), VALUE "sling_project_version", VALUE "sling_project_title")
			return true
		}
		else if arg.isFlag("verbose") {
			verbose = true
			ctx.setEnableDebugMessages(true)
		}
		else if arg.isFlag("nodata") {
			nodata = true
		}
		else if arg.isOption("db") {
			connectionString = arg.getStringValue()
		}
		else if arg.isOption("query") {
			sqlQuery = arg.getStringValue()
		}
		else if arg.isOption("execute") {
			sqlExecute = arg.getStringValue()
		}
		else if arg.isFlag("tables") {
			listTables = true
		}
		else if arg.isFlag("count") {
			count = true
		}
		else if arg.isFlag("stream") {
			stream = true
		}
		else if arg.isOption("paramString") {
			params += arg.getStringValue()
		}
		else if arg.isOption("paramInteger") {
			params += Integer.asObject(String.toInteger(arg.getStringValue()))
		}
		else if arg.isOption("paramLong") {
			params += LongInteger.asObject(String.toLong(arg.getStringValue()))
		}
		else if arg.isOption("paramDouble") {
			params += Double.asObject(String.toDouble(arg.getStringValue()))
		}
		else if arg.isOption("paramBlob") {
			var ff = File.forPath(arg.getStringValue())
			assert ff.exists():
				Log.error(ctx, "File does not exist: `" .. ff.getPath() .. "'")
			var data = assert ff.getContentsBuffer():
				Log.error(ctx, "Failed to read file: `" .. ff.getPath() .. "'")
			params += data
		}
		else {
			arg.reportAsUnsupported(ctx)
			return false
		}
	}
	var start = SystemClock.asTimeValue()
	assert String.isNotEmpty(connectionString):
		Log.error(ctx, "No connection string specified. Use -db=.. to specify.")
	trace("Opening database connection")
	var db = assert SQLDatabaseFactory.getDatabase(ctx, connectionString):
		Log.error(ctx, "Failed to initialize database connection: `" .. connectionString .. "'")
	trace("Database connection opened.")
	var v = false
	if listTables {
		startOperation("Retrieve database tables")
		var tabs = db.queryAllTableNamesSync()
		endOperation()
		startOperation("Sorting table array")
		var da = DynamicVector.forObjectVector(tabs)
		da.sort()
		tabs = da.toVector()
		endOperation()
		trace("Total of " .. String.forInteger(Vector.getSize(tabs)) .. " tables found")
		startOperation("Printing table names")
		if not nodata {
			var longestName = 0
			foreach tab as string in tabs {
				var ll = String.getLength(tab)
				if ll > longestName:
					longestName = ll
			}
			foreach tab as string in tabs {
				if count {
					startOperation("Counting records in " .. tab)
					var sv = db.querySingleRowSync(db.prepareSync("SELECT COUNT(*) as count FROM \"" .. tab .. "\";"))
					endOperation()
					var count = "unknown"
					if sv:
						count = sv.getString("count")
					if count != "unknown":
						count = StringUtil.formatNumberString(count, ',')
					stdout.println(StringUtil.padToLength(tab, longestName) .. " : " .. StringUtil.padToLength(count, 20, 1))
				}
				else {
					stdout.println(tab)
				}
			}
		}
		endOperation()
	}
	else if sqlQuery {
		startOperation("Preparing SQL query")
		var stmt = db.prepareSync(sqlQuery)
		addParamsToStatement(stmt)
		endOperation()
		if not stmt {
			Log.error(ctx, "Failed to prepare SQL statement: `" .. sqlQuery .. "'")
		}
		else {
			startOperation("Executing SQL query")
			var sv = db.querySync(stmt)
			endOperation()
			if not sv {
				Log.error(ctx, "SQL query failed: `" .. sqlQuery .. "'")
			}
			else {
				if stream {
					startOperation("Streaming JSON records")
					if not nodata:
						stdout.print("[")
					var sb = new StringBuilder()
					sv.headerJSON(sb)
					if not nodata:
						stdout.print(sb.toString())
					loop {
						sb.clear()
						if not sv.nextJSON(sb):
							break
						var s = sb.toString()
						if not nodata:
							stdout.print(s)
					}
					if not nodata:
						stdout.print("]")
					endOperation()
				}
				else {
					startOperation("Reading results as JSON")
					var jsondata = sv.toVectorListJSON()
					endOperation()
					trace("JSON String length: " .. StringUtil.formatInteger(String.getLength(jsondata), ',') .. " bytes")
					if not nodata {
						startOperation("Printing results on the console")
						stdout.print(jsondata)
						endOperation()
					}
				}
			}
			startOperation("Closing prepared statement")
			stmt.close()
			endOperation()
			v = true
		}
	}
	else if sqlExecute {
		startOperation("Preparing SQL query")
		var stmt = db.prepareSync(sqlExecute)
		addParamsToStatement(stmt)
		endOperation()
		if not stmt {
			Log.error(ctx, "Failed to prepare SQL statement: `" .. sqlExecute .. "'")
		}
		else {
			startOperation("Executing SQL query")
			var sv = db.executeSync(stmt)
			endOperation()
			startOperation("Closing prepared statement")
			stmt.close()
			endOperation()
			if not sv {
				Log.error(ctx, "SQL query failed: `" .. sqlExecute .. "'")
			}
			else {
				Log.info(ctx, "SQL query successful: `" .. sqlExecute .. "'")
			}
		}
		v = true
	}
	else {
		Log.error(ctx, "No sql statement to execute. Use either -tables, -query=.. or -execute=.. to specify.")
		v = false
	}
	startOperation("Closing database connection")
	db.closeSync()
	endOperation()
	var tdiff = TimeValue.diff(SystemClock.asTimeValue(), start)
	trace("Operation ended: Total execution time: " .. StringUtil.formatLongInteger(tdiff, ',') .. " us")
	return v
}
